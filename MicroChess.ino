/**
 * ArduinoChess.ino
 * 
 * the MicroChess project: https://github.com/ripred/MicroChess
 * 
 * written March 2023 - Trent M. Wyatt
 * 
 * version 1.0.0
 *   First base
 * 
 * version 1.1.0
 * Modified board_t to save 16 bytes
 * 
 * version 1.2.0
 * Added evaluation
 * 
 * version 1.3.0
 * Added storage for pieces and moves, printf adjustable print debugging
 * 
 * version 1.4.0
 * Added board traversal, pawn movements
 * 
 * version 1.5.0
 * Added knight movements
 * 
 * version 1.6.0
 * Added move execution
 * 
 * version 1.7.0
 * Major bug fixes in piece tracking and move generation
 * 
 * version 1.8.0
 * Added Rook, Bishop, Queen, and King move generation
 * Added 'in check' attributes
 * Added 'has moved' attributes
 * 
 * version 1.9.0
 * 
 *  [+] fix and test the new Rook, Bishop, Queen, and King pieces
 *  [+] create new macro for printf that automatically declares the
 *      format string as a PROGMEM array behind the scenes without
 *      requiring a PROGMEM to be decared at each use of printf(...)!
 *  [+] replace all checks for Empty == getType(p) with isEmpty(p)
 *  [+] add the ability turn off all output in order to profile 
 *      the engine without waiting on serial i/o.
 *  [+] move the offsets into PROGMEM.
 *  [+] add pawn promotion to queen when reaching the last row.
 *  [+] move the move generation for each Piece type into it's own function.
 *  [+] create a separate file for each piece.
 *  [+] add precomputed tables for the material bonuses calculated by evaluate(...) at runtime.
 *  [+] add precomputed tables for the center bonuses calculated by evaluate(...) at runtime.
 *  [+] add the time spent and number of moves evaluated on the last move to the status area
 * 
 * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
 * TODO: for version 1.10.0
 * 
 *  [+] change the api for the individual Piece generation functions so that
 *      the same "best move" move_t object is passed throughout and remove
 *      the best move objects from the main game_t!
 *
 *  [+] add ply level awareness and recursion.
 *  [+] get rid of move lists!
 *  [+] evaluate each move as it is generated by the pieces, remember the best one!
 *  [+] add castling.
 *  [+] update the show_move(...) function to properly display when a pawn executes 
 *      an en-passant capture. ?
 *  [+] add and implement a "deleted" flag for pieces in the game.pieces[] list so they are ignored
 *      during moves that take pieces instead of needing to move/copy the taken piece from the list
 *  [ ] add minimax algorithm.
 *  [ ] add alpha-beta pruning.
 *  [ ] add reading and writing of FEN notation.
 *  [ ] 
 * 
 * BUGBUGS: to fix!
 * 
 *  [ ] 
 * 
 * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
 * 
 */
#include <Arduino.h>
#include "MicroChess.h"

////////////////////////////////////////////////////////////////////////////////////////
// the game board
board_t board;

////////////////////////////////////////////////////////////////////////////////////////
// the currently running game
game_t game;

////////////////////////////////////////////////////////////////////////////////////////
// Function to check if a move is better than the current best move
// 
// returns True if move is better, False otherwise
Bool is_better_move(
    move_t const &move, 
    move_t const &best, 
    Bool const use_random, 
    Color const side) {

    if (-1 == move.from) {
        return True;
    }

    if (side == White) {
        if (move.value > best.value && (MAX_VALUE != move.value)) {
            return True;
        }
        else if (move.value == best.value) {
            return use_random && random(2);
        }
    }
    else {
        if (move.value < best.value && (MIN_VALUE != move.value)) {
            return True;
        }
        else if (move.value == best.value) {
            return use_random && random(2);
        }
    }

    return False;

}   // is_better_move(...)


////////////////////////////////////////////////////////////////////////////////////////
// Consider a move against the best white move or the best black move
// depending on the color of the piece and set this as the best move
// if it has a higher value (or an equal value when we're using random)
// 
// returns True if the move is the new best move, False otherwise
Bool consider_move(move_t &move, move_t &best) 
{
    if (freeMemory() < game.options.low_mem_limit) {
        digitalWrite(DEBUG1_PIN, HIGH);
        delayMicroseconds(200);
        digitalWrite(DEBUG1_PIN, LOW);
        return False;
    }

    Piece const p = board.get(move.from);
    Color const side = getSide(p);

    // Make the move_t object for the move (recursively) and it's evaluation
    move.value = make_move(move, True);

    // penalize the move if it would cause us to lose by move repetition
    if (would_repeat(move)) {
        move.value += ((White == side) ? -1 : +1) * 5000;
    }

    // check if the move is better than the current best move
    if (is_better_move(move, best, game.options.random, side)) {
        best = move;
        return True;
    }

    return False;

}   // consider_move(move_t &move, move_t &best)


////////////////////////////////////////////////////////////////////////////////////////
// Find the piece index for a given board index.
// 
// returns the index into the game.pieces[] array for the specified piece
inline index_t find_piece(index_t const index)
{
    for (index_t piece_index = 0; piece_index < game.piece_count; piece_index++) {
        point_t const &loc = game.pieces[piece_index];
        if (-1 == loc.x || -1 == loc.y) {
            continue;
        }
        else {
            index_t const board_index = loc.x + loc.y * 8;
            if (board_index == index) {
                return piece_index;
            }
        }
    }

    return -1;

}   // find_piece(int const index)


////////////////////////////////////////////////////////////////////////////////////////
// Move a piece on the board, taking a piece if necessary. Evaluate the value of the 
// board after the move. Optionally restore the board back to it's original state after
// evaluating the value of the move.
// 
// This is a big and complicated function.
// It performs 5 major steps:
// 
//  1) Identify the piece being moved
//  2) Identify any piece being captured and remove it if so
//  3) Place the piece being moved at the destination
//  4) Evaluate the board score after making the move
//  5) If we are just considering the move then put everything back
// 
// returns the value of the board after the move was made
long make_move(move_t const &move, Bool const restore) 
{
    // The value of the board.
    // Default to the worst value for our side.
    // i.e: Don't make the move whatever it is
    long value = (White == game.turn) ? MIN_VALUE : MAX_VALUE;

    /// Step 1: Identify the piece being moved

    // Get the attributes for the piece being moved
    index_t const col = move.from % 8;
    index_t const row = move.from / 8;

    // Check for a deleted piece and skip it if so
    if (-1 == col || -1 == row) {
        return value;
    }

    struct {
        uint8_t const from : 6,
                     piece : 8,
                      type : 3,
                      side : 1,
               whites_turn : 1,

        // Get the attributes for the destination location
                    to_col : 3,
                    to_row : 3,
                        to : 6,

        // Get the attributes for any possible piece being taken at the destination location
                        op : 8,
                     otype : 3,
                     oside : 1;
    } vars = { 
        uint8_t(move.from), 
        board.get(vars.from), 
        getType(vars.piece), 
        getSide(vars.piece), 
        White == vars.side, 
        uint8_t(move.to % 8), 
        uint8_t(move.to / 8), 
        uint8_t(move.to),
        board.get(vars.to),
        getType(vars.op),
        getSide(vars.op)
    };

    long const worst_value = vars.whites_turn ? MIN_VALUE : MAX_VALUE;
    long const best_value = vars.whites_turn ? MAX_VALUE : MIN_VALUE;

    // Ensure that we aren't trying to take our own piece
    // BUGBUG: Remove after testing
    if (Empty != vars.otype && vars.side == vars.oside) {
        // printf(Debug1, "\nattempt to take our own piece at line %d\n", __LINE__);
        // while ((true)) {}
        return worst_value;
    }

    // Save the current last 5 moves in the game history
    move_t history[MAX_REPS * 2 - 1];
    index_t hist_count = game.hist_count;    

    // save the current king locations
    index_t const wking = game.wking;
    index_t const bking = game.bking;

    if (restore) {
        memmove(history, game.history, sizeof(history));

        // Track the number of moves considered so far
        game.stats.inc_moves_count();

        // Track the max number of moves generated during a turn
        uint32_t const move_count = game.stats.move_count_so_far();

        if (vars.side == game.turn) {
            if (move_count > game.stats.max_moves) {
                game.stats.max_moves = move_count;
            }
        }
    }

    // See if this places the opponent King in check
    if (King == vars.otype) {
        if (vars.whites_turn && Black == vars.oside) {
            game.black_king_in_check = True;
            value = best_value;
            // return best_value;
        }
        else if (!vars.whites_turn && White == vars.oside) {
            game.white_king_in_check = True;
            value = best_value;
            // return best_value;
        }
    }

    // Find the index of the piece being moved in the array of pieces in the game:
    index_t const piece_index = find_piece(vars.from);


    /// Step 2: Identify any piece being captured and remove it if so.
    // 
    // * NOTE BELOW *
    // Once any changes have been made to the board or game state we
    // MUST NOT return until passing through the "if (restore) { ... }" logic.

    // The game.pieces[] index being captured (if any, -1 if none)
    index_t taken_index = -1;

    // The board index being captured (if any, -1 if none)
    index_t captured = -1;

    // save the current number of taken pieces
    index_t const white_taken_count = game.white_taken_count;
    index_t const black_taken_count = game.black_taken_count;

    // save the current last move and move flags
    Bool const last_was_en_passant = game.last_was_en_passant;
    Bool const last_was_castle = game.last_was_castle;
    move_t const last_move = game.last_move;

    Piece captured_piece = Empty;

    if (Pawn == vars.type && isEmpty(vars.otype) && col != vars.to_col) {
        // en-passant capture
        game.last_was_en_passant = True;
        captured = vars.to_col + row * 8;
        captured_piece = board.get(captured);
    }

    if (-1 == captured) {
        // See if the destination is not empty and not a piece on our side.
        // i.e. an opponent's piece.
        if (Empty != vars.otype && vars.side != vars.oside) {
            captured = vars.to;
            captured_piece = board.get(captured);
        }
    }

    // If a piece was taken, make the change on the board and to the game.pieces[] list
    if (-1 != captured) {
        // remember the piece index of the piece being taken
        taken_index = find_piece(captured);

        // change the spot on the board for the taken piece to Empty
        board.set(captured, Empty);

        // Soft-delete the piece taken in the piece list!
        game.pieces[taken_index] = { -1, -1 };

        // add the piece to the list of taken pieces
        if (vars.whites_turn) {
            game.taken_by_white[game.white_taken_count++] = captured_piece;
        }
        else {
            game.taken_by_black[game.black_taken_count++] = captured_piece;
        }
    }


    /// Step 3: Place the piece being moved at the destination

    Piece place_piece = setMoved(vars.piece, True);

    // Promote a pawn to a queen if it reached the back row
    if (Pawn == vars.type && (vars.to_row == ((vars.whites_turn) ? index_t(0) : index_t(7)))) {
        place_piece = setType(place_piece, Queen);
        game.last_was_pawn_promotion = True;
    }

    // Move the piece to the destination on the board
    board.set(vars.from, Empty);
    board.set(vars.to, place_piece);

    // move the piece in the piece list
    game.pieces[piece_index] = { index_t(vars.to_col), index_t(vars.to_row) };

    // Castling and keeping track of the king's locations
    bool const last_move_was_castle = game.last_was_castle;
    index_t castly_rook = -1;
    if (King == vars.type) {
        if (White == vars.side) {
            game.wking = vars.to;
        }
        else {
            game.bking = vars.to;
        }

        // get the horizontal distance the king is moving
        index_t dist = abs(vars.to_col - (vars.from % 8));

        // see if it is a castling move
        if (dist >= 2) {
            // see which side we're castling on
            if (2 == dist) {
                // castle on the King's side
                index_t board_rook = 7 + (vars.from / 8) * 8;
                castly_rook = find_piece(board_rook);
                board.set(board_rook, setMoved(board.get(board_rook), True));
                game.pieces[castly_rook].x = 5;
                game.last_was_castle = True;
            }
            else if (3 == dist) {
                // castle on the Queen's side
                index_t board_rook = 0 + (vars.from / 8) * 8;
                castly_rook = find_piece(board_rook);
                board.set(board_rook, setMoved(board.get(board_rook), True));
                game.pieces[castly_rook].x = 3;
                game.last_was_castle = True;
            }
            else {
                // error
                printf(Debug1, "Error: Invalid King move at line %d\n", __LINE__);
                while ((true)) {}
            }
        }
    }


    // Step 4: Evaluate the board score after making the move


    value = evaluate();

    if (!restore && value != MIN_VALUE && value != MAX_VALUE) {
        game.last_value = value;
    }

    // set our move as the last move
    game.last_move = move;

    ////////////////////////////////////////////////////////////////////////////////////////
    // The move has been made and we have the value for the updated board.
    // Recursively look-ahead and accumulatively update the value here.
    // 
    // This is working in brute-force mode!!
    // 
    // TODO: Implement minimax and alpha-beta pruning!!
    // 
    // Minimax Algorithm Pseudo-code:
    // 
    // function minimax(node, depth, maximizingPlayer) is
    //     if depth = 0 or node is a terminal node then
    //         return the heuristic value of node
    //     if maximizingPlayer then
    //         value := −∞
    //         for each child of node do
    //             value := max(value, minimax(child, depth − 1, FALSE))
    //         return value
    //     else (* minimizing player *)
    //         value := +∞
    //         for each child of node do
    //             value := min(value, minimax(child, depth − 1, TRUE))
    //         return value
    // 
    // (* Initial call *)
    // minimax(origin, depth, TRUE)
    // 
    ////////////////////////////////////////////////////////////////////////////////////////

    #define   MAXMAX_PLY   4

    Bool const low_mem = freeMemory() < game.options.low_mem_limit;
    if (low_mem) {
        digitalWrite(DEBUG1_PIN, HIGH);
        delayMicroseconds(200);
        digitalWrite(DEBUG1_PIN, LOW);
    }
    // no need to explore future plies if we've already made our mind up
    else if (restore) {
        // flag indicating whether we are traversing into quiescent moves
        Bool const quiescent = ((-1 != captured) && (game.ply < (game.options.maxply + 2)) && (game.ply < MAXMAX_PLY));

        if (game.ply < game.options.maxply || quiescent) {
            // We check for timeouts here AFTER at least evaluating the current move.
            // That way, we don't stop without any knowledge of half of the pieces
            // halfway accross the board IF any timeouts do occur.
            // 
            // Note that we also require letting the ply level go to AT LEAST LEVEL 1 so
            // that we always allow the setting of the 'king-in-check' flags for any moves,
            // based on whether any of the opponent's first-level responses place the king in check.

            game.last_was_timeout = timeout();
            digitalWrite(DEBUG3_PIN, game.last_was_timeout);

            if (!game.last_was_timeout || (game.ply < 1)) {
                // optionally flash a 'quiescent' indicator
                if (game.options.live_update) {
                    digitalWrite(DEBUG2_PIN, HIGH);
                    delayMicroseconds(1);
                    digitalWrite(DEBUG2_PIN, LOW);
                }

                // Explore the future! (plies)
                game.ply++;
                ++game.turn %= 2;

                if (game.ply > game.stats.move_stats.maxply) {
                    game.stats.move_stats.maxply = game.ply;
                }

                Bool const white_king_in_check = game.white_king_in_check;
                Bool const black_king_in_check = game.black_king_in_check;

                reset_move_flags();

                if (vars.whites_turn) {
                    move_t best_black = { -1, -1, MAX_VALUE };
                    choose_best_move(Black, best_black, consider_move);
                    value += best_black.value;
                }
                else {
                    move_t best_white = { -1, -1, MIN_VALUE };
                    choose_best_move(White, best_white, consider_move);
                    value = best_white.value;
                }

                // punish moves that place our King in check
                // if (game.ply >= 1) {
                //     if ((vars.whites_turn && game.white_king_in_check) || 
                //     (!vars.whites_turn && game.black_king_in_check)) {
                //         value += worst_value;
                //     }
                // }

                game.white_king_in_check = white_king_in_check;
                game.black_king_in_check = black_king_in_check;

                ++game.turn %= 2;
                game.ply--;
            }
        }
    }

    // periodically update the LED strip display if enabled
    if (game.options.live_update && (game.ply < game.options.maxply)) {
        static move_t last_update = { -1, -1, 0 };
        if (last_update.from != move.from || last_update.to != move.to) {
            last_update = move;
            set_led_strip();
        }
    }


    // Step 5: If we are just considering the move then put everything back

    if (restore) {
        if (-1 == captured) {
            board.set(vars.to, vars.op);
        } else {
            index_t const captured_col = captured % 8;
            index_t const captured_row = captured / 8;

            // restore the captured board changes and
            // set it's "in-check" flag
            captured_piece = setCheck(captured_piece, True);
            board.set(captured, captured_piece);

            // restore the captured piece list changes
            game.pieces[taken_index] = { captured_col, captured_row };
        }

        // restore the taken pieces list changes
        game.white_taken_count = white_taken_count;
        game.black_taken_count = black_taken_count;

        // restore the changes made to the moves history
        memmove(game.history, history, sizeof(history));
        game.hist_count = hist_count;

        // restore the moved piece board changes
        board.set(vars.from, vars.piece);

        // restore the moved piece pieces list changes
        game.pieces[piece_index] = { col, row };

        // restore the last move made
        game.last_move = last_move;

        // restore the en passant
        game.last_was_en_passant = last_was_en_passant;
        game.last_was_castle = last_was_castle;

        // restore the king's locations
        game.wking = wking;
        game.bking = bking;

        // restore any rook moved during a castle move
        game.last_was_castle = last_move_was_castle;
        if (-1 != castly_rook) {
            if (game.pieces[castly_rook].x == 3) {
                game.pieces[castly_rook].x = 0;
            }
            else {
                game.pieces[castly_rook].x = 7;
            }

            index_t const rook = game.pieces[castly_rook].x + game.pieces[castly_rook].y * 8;
            board.set(rook, setMoved(board.get(rook), False));
        }
    }

    return value;

}   // make_move(move_t const &move, Bool const restore)


////////////////////////////////////////////////////////////////////////////////////////
// Evaluate the identity (score) of the board state.
// Positive scores indicate an advantage for white and
// Negative scores indicate an advantage for black.
// Uses pre-computed material bonus tables for speed.
// 
// returns the score/value of the current board
long evaluate() 
{
    // calculate the value of the board
    long materialTotal = 0L;
    long mobilityTotal = 0L;
    long centerTotal = 0L;
    long kingTotal = 0L;
    long score = 0L;

    for (index_t piece_index = 0; piece_index < game.piece_count; piece_index++) {
        index_t const col = game.pieces[piece_index].x;
        index_t const row = game.pieces[piece_index].y;
        if (-1 == col || -1 == row) continue;

        Piece   const p = board.get(col + row * 8);
        Piece   const ptype = getType(p);
        Color   const pside = getSide(p);

        if (Empty == ptype) continue;

        // material bonus
        if ((true)) {
            materialTotal += pgm_read_dword(&game.material_bonus[ptype][pside]);
        }

        // Let's not encourage the King to wander to
        // the center of the board mmkay?
        if (King == ptype) {
            continue;
        }

        // center bonus
        centerTotal +=
            pgm_read_dword(&game.center_bonus[col][ptype][pside]) +
            pgm_read_dword(&game.center_bonus[row][ptype][pside]);

        // proximity to opponent's King
        if (White == pside) {
            kingTotal += abs((game.bking % 8) - (7 - col)) + abs((game.bking / 8) - (7 - row));
        }
        else {
            kingTotal += abs((game.wking % 8) - (7 - col)) + abs((game.wking / 8) - (7 - row));
        }
    }

    // The score or 'identity property' of the board can include extra points for
    // how many total moves (mobility) the remaining pieces can make
    // if (filter & mobility) {
    //     long sideFactor = (Black == side) ? -1 : 1;
    //     mobilityTotal += static_cast<long>(game.move_count1 * mobilityBonus * sideFactor);
    //     mobilityTotal -= static_cast<long>(game.move_count2 * mobilityBonus * sideFactor);
    // }

    kingTotal *= game.options.kingBonus;

    score = kingTotal + materialTotal + centerTotal + mobilityTotal;

    // printf(Debug4, 
    //     "evaluation: %ld = centerTotal: %ld  materialTotal: %ld  mobilityTotal: %ld\n", 
    //     score, centerTotal, materialTotal, mobilityTotal);

    return score;

}   // evaluate()


////////////////////////////////////////////////////////////////////////////////////////
// reset the various move tracking flags
void reset_move_flags() 
{
    for (index_t ndx = 0; ndx < game.piece_count; ndx++) {
        index_t const col = game.pieces[ndx].x;
        index_t const row = game.pieces[ndx].y;
        if (-1 == col || -1 == row) {
            continue;
        }
        else {
            index_t const from = col + row * 8;
            Piece   const p = board.get(from);
            board.set(from, setCheck(p, False));
        }
    }

    // reset the king-in-check flags
    game.white_king_in_check = False;
    game.black_king_in_check = False;

    game.last_was_en_passant = False;
    game.last_was_castle = False;
    game.last_was_timeout = False;
    game.last_was_pawn_promotion = False;

}   // reset_move_flags()


////////////////////////////////////////////////////////////////////////////////////////
// Evaluate all of the available moves for the specified side.
// The best move is stored in best.
// The callback is called for each move.
// 
// returns the number of pieces that can move,
// which is also the number of moves stored in the *moves array
// if the pointer is not nullptr.
void choose_best_move(Color const who, move_t &best, generator_t callback)
{
    Bool const low_mem = freeMemory() < game.options.low_mem_limit;
    if (low_mem) {
        digitalWrite(DEBUG1_PIN, HIGH);
        delayMicroseconds(200);
        digitalWrite(DEBUG1_PIN, LOW);
        return;
    }

    static Bool constexpr   enable_pawns = True;
    static Bool constexpr enable_knights = True;
    static Bool constexpr enable_bishops = True;
    static Bool constexpr   enable_rooks = True;
    static Bool constexpr  enable_queens = True;
    static Bool constexpr   enable_kings = True;

    Bool const whites_turn = (White == who) ? True : False;
    long const worst_value = whites_turn ? MIN_VALUE : MAX_VALUE;

    // walk through the pieces list and generate all moves for each piece
    for (index_t ndx = 0; ndx < game.piece_count; ndx++) {
        index_t const col = game.pieces[ndx].x;
        index_t const row = game.pieces[ndx].y;
        if (-1 == col || -1 == row) continue;

        index_t const from = col + row * 8;
        Piece   const piece = board.get(from);
        Piece   const type = getType(piece);
        Color   const side = getSide(piece);

        // track the location of both kings
        if (King == type) {
            if (White == side) {
                game.wking = from;
            }
            else {
                game.bking = from;
            }
        }

        // check for move timeout
        if (timeout() && game.ply >= 2) {
            game.last_was_timeout = True;
            return;
        }

        if (Empty == type || side != who) { continue; }

        // construct a move_t object with the starting location
        move_t move = { from, 0, worst_value };
        move_t best_piece_move = { -1, -1, worst_value };
        piece_gen_t gen(move, best_piece_move, callback);

        switch (type) {
            case   Pawn: if ((enable_pawns))   {   add_pawn_moves(gen); }  break;
            case Knight: if ((enable_knights)) { add_knight_moves(gen); }  break;
            case Bishop: if ((enable_bishops)) { add_bishop_moves(gen); }  break;
            case   Rook: if ((enable_rooks))   {   add_rook_moves(gen); }  break;
            case  Queen: if ((enable_queens))  {  add_queen_moves(gen); }  break;
            case   King: if ((enable_kings))   {   add_king_moves(gen); }  break;

            default:
                printf(Debug1, "error: invalid type = %d at line %d\n", type, __LINE__);
                game.options.print_level = Debug1;
                show();
                game.stats.stop_game_stats();
                show_stats();
                while ((1)) {}
                break;
        }

        // All of the moves for this one piece have been generated and evaluated and the best move
        // is stored in 'best_piece_move'. If the best move for the piece is legal then compare
        // it against the best move so far and update it if it is better.
        if (-1 != best_piece_move.from && -1 != best_piece_move.to) {
            if ((whites_turn && best_piece_move.value > best.value) || (!whites_turn && best_piece_move.value < best.value)) {
                best = best_piece_move;
            }
            else 
            if ((best_piece_move.value == best.value) && game.options.random && random(2)) {
                best = best_piece_move;
            }
        }

    } // for each piece for this side..

}   // choose_best_move()


////////////////////////////////////////////////////////////////////////////////////////
// Make the next move in the game until we reach a stalemate or checkmate
// 
void play_game()
{
    show();

    Bool const whites_turn = (White == game.turn) ? True : False;

    game.stats.start_move_stats();

    reset_move_flags();

    move_t best_white = { -1, -1, MIN_VALUE };
    Bool no_white_moves = (-1 == best_white.from);

    move_t best_black = { -1, -1, MAX_VALUE };
    Bool no_black_moves = (-1 == best_black.from);

    // reset the ply depth watermark
    game.stats.move_stats.maxply = 0;

    if (whites_turn) {
        // get the best move and flags for white
        choose_best_move(White, best_white, consider_move);
        no_white_moves = (-1 == best_white.from);
    }
    else
    {
        // get the best move and flags for black
        choose_best_move(Black, best_black, consider_move);
        no_black_moves = (-1 == best_black.from);
    }

    // gather the move statistics
    game.stats.stop_move_stats();
    game.last_move_time = game.stats.move_stats.duration();
    game.last_moves_evaluated = game.stats.move_stats.counter();

    // see if we've hit the move limit
    if (game.move_num >= game.options.move_limit) {
        game.state = FIFTY_MOVES;
        return;
    }

    move_t const &move = whites_turn ? best_white : best_black;

    // Display the move that we chose * Before Modifying the Board *
    printf(Debug1, "\nMove #%d: ", game.move_num + 1);
    show_move(move);

    // Make the move:
    make_move(move, False);

    if (game.last_was_en_passant) {
        printf(Debug1, " en passant capture ")
    }

    if (game.last_was_pawn_promotion) {
        printf(Debug1, " pawn promoted ")
    }

    if (game.last_was_timeout) {
        printf(Debug1, " - timeout ")
    }

    if (game.last_was_castle) {
        printf(Debug1, " castling ")
    }

    printf(Debug1, "\n");

    // Announce if either king is in check
    if (game.white_king_in_check) {
        printf(Debug1, "White King is in check!\n");
        if (whites_turn) {
            printf(Debug1, "illegal move\n");
        }
    }

    if (game.black_king_in_check) {
        printf(Debug1, "Black King is in check!\n");
        if (!whites_turn) {
            printf(Debug1, "illegal move\n");
        }
    }

    printf(Debug1, "\n");

    // check for move repetition
    if ((PLAYING == game.state) && add_to_history(move)) {
        game.state = whites_turn ? WHITE_3_MOVE_REP : BLACK_3_MOVE_REP;
    }

    if (whites_turn && game.white_king_in_check) {
        no_white_moves = True;
    }

    if (!whites_turn && game.black_king_in_check) {
        no_black_moves = True;
    }

    // see if the game has been won
    if ((PLAYING == game.state) && whites_turn && no_black_moves && game.black_king_in_check) {
        game.state = WHITE_CHECKMATE;
    }

    if ((PLAYING == game.state) && !whites_turn && no_white_moves && game.white_king_in_check) {
        game.state = BLACK_CHECKMATE;
    }

    // see if we have a stalemate
    if ((PLAYING == game.state) && no_white_moves && no_black_moves) {
        game.state = STALEMATE;
    }

    // toggle whose turn it is
    ++game.turn %= 2;

    // increase the game moves counter
    game.move_num++;

    // Now go through the piece list and actually remove any pieces that
    // were soft-deleted during evaluation when the physical layout of
    // the list couldn't be modified.
    for (index_t i = 0; i < game.piece_count; i++) {
        if (-1 == game.pieces[i].x) {
            game.pieces[i] = game.pieces[--game.piece_count];
        }
    }

}   // play_game()


////////////////////////////////////////////////////////////////////////////////////////
// Set all of the options for the game
// 
void set_game_options()
{
    // set game.options.profiling to True (1) to disable output and profile the engine
    game.options.profiling = False;
    // game.options.profiling = True;

    // set the maximum ply level (the number of turns we look ahead) for the game
    game.options.maxply = 2;

    // set the limit on the total number of moves allowed in the game
    // Officially the limit is 50 moves
    game.options.move_limit = 200;

    // set the optional time limit on each move in milliseconds
    game.options.time_limit = 240000;

    // set game.options.random to True (1) to use randomness in the game decisions
    // game.options.random = False;
    game.options.random = True;

    // set whether we play continuously or not
    // game.options.continuous = game.options.random;
    game.options.continuous = True;
    // game.options.continuous = False;

    // set the low-memory watermark we use to limit recursion
    game.options.low_mem_limit = 64;

    // set the 'live update' flag
    game.options.live_update = True;

    // game seed hash for PRN generator - default to 4 hex prime numbers
    game.options.seed = 0x232F89A3;
    uint16_t upper = game.options.seed >> 16;
    uint16_t lower = word(game.options.seed);

    // Enable random seed when program is debugged.
    // Disable random seed to reproduce issues or to profile.
    if (game.options.profiling) {
        printf(Debug1, "profiling. ");

        // game.options.random = False;
        game.options.print_level = None;
    } 

    // Salt the psuedo-random number generator seed if enabled:
    if (game.options.random) {
        // Add salt to the psuedo random number generator seed
        // from the physical environment
        uint8_t const pins[] = { 2, 7, 8, 9, 10, 11, 12 };
        uint8_t const total_passes = random(23, 87);
        uint32_t some_bits = 1234567890;

        randomSeed(random());

        for (uint8_t pass = 0; pass < total_passes; pass++) {
            for (uint8_t pin = 0; pin < ARRAYSZ(pins); pin++) {
                pinMode(pins[pin], INPUT);
                some_bits ^= digitalRead(pins[pin]) << (analogRead(A2) % 42u);                
            }
        }
        uint8_t bits = (game.options.seed >> 11) & 0xFF;
        game.options.seed += 
            bits +
            (uint32_t(analogRead(A0)) << 24) +
            (uint32_t(analogRead(A1)) << 16) +
            (uint32_t(analogRead(A2)) << 17) +
            (uint32_t(analogRead(A3)) << 11) +
            uint32_t(analogRead(A4)) +
            uint32_t(micros());

        game.options.seed += some_bits;        

        upper = game.options.seed >> 16;
        lower = word(game.options.seed);
    }

    printf(Always, "prng seed hash: 0x%04X%04X\n", upper, lower);
    printf(Always, "nominal ply limit: %d\n", game.options.maxply);

    char str[16];
    ftostr(game.options.time_limit, 0, str);
    printf(Always, "time limit: %s ms\n\n", str);

    randomSeed(game.options.seed);

}   // set_game_options()


////////////////////////////////////////////////////////////////////////////////////////
// Continually call play_game() until we reach the end of the game.
// Display the statistics for the game and start another game.
// 
// int main(int /* argc */, char */* argv*/[] ) 
void setup()
{
    Serial.begin(115200); while (!Serial); Serial.write('\n');

    init_led_strip();

    static uint8_t const pins[3] = { DEBUG1_PIN, DEBUG2_PIN, DEBUG3_PIN };
    for (uint8_t pin : pins) {
        pinMode(pin, OUTPUT);
        digitalWrite(pin, LOW);
    }

    uint32_t state_totals[6] = { 0, 0, 0, 0, 0, 0 };
    uint32_t white_wins = 0;
    uint32_t black_wins = 0;

    do {
        set_game_options();

        // initialize the board and the game:
        board.init();
        game.init();

        game.stats.start_game_stats();

        do {
            play_game();

        } while (PLAYING == game.state);

        game.stats.stop_game_stats();

        game.options.print_level = Debug1;

        switch (game.state) {
            case STALEMATE:         printf(Debug1, "Stalemate\n\n");                                        break;
            case WHITE_CHECKMATE:   printf(Debug1, "Checkmate! White wins!\n\n");                           break;
            case BLACK_CHECKMATE:   printf(Debug1, "Checkmate! Black wins!\n\n");                           break;
            case WHITE_3_MOVE_REP:  printf(Debug1, "%d-move repetition! Black wins!\n\n", MAX_REPS);        break;
            case BLACK_3_MOVE_REP:  printf(Debug1, "%d-move repetition! White wins!\n\n", MAX_REPS);        break;
            case FIFTY_MOVES:       printf(Debug1, "%d-move limit reached!\n\n", game.options.move_limit);  break;
            default: 
            case PLAYING:           break;
        }

        // show the final board    
        show();

        // print out the game move counts and time statistics
        show_stats();

        state_totals[game.state - 1]++;
        char str[16] = "";

        printf(Debug1, "         Stalemate   White Checkmate   Black Checkmate  White %d-Move Rep  Black %d-Move Rep        Move Limit\n", 
            MAX_REPS, MAX_REPS);
        
        ftostr(state_totals[       STALEMATE - 1], 0, str);
        printf(Debug1, "%18s", str);
        ftostr(state_totals[ WHITE_CHECKMATE - 1], 0, str);
        printf(Debug1, "%18s", str);
        ftostr(state_totals[ BLACK_CHECKMATE - 1], 0, str);
        printf(Debug1, "%18s", str);
        ftostr(state_totals[WHITE_3_MOVE_REP - 1], 0, str);
        printf(Debug1, "%18s", str);
        ftostr(state_totals[BLACK_3_MOVE_REP - 1], 0, str);
        printf(Debug1, "%18s", str);
        ftostr(state_totals[     FIFTY_MOVES - 1], 0, str);
        printf(Debug1, "%18s", str);

        printf(Debug1, "\n");

        switch (game.state) {
            default:
            case PLAYING:
            case STALEMATE:
            case FIFTY_MOVES:
                break;

            case WHITE_CHECKMATE:
            case BLACK_3_MOVE_REP:
                white_wins++;
                break;

            case BLACK_CHECKMATE:
            case WHITE_3_MOVE_REP:
                black_wins++;
                break;
        }

        printf(Debug1, "   White wins: %3ld   Black wins: %3ld\n\n", white_wins, black_wins)
    
    } while (game.options.continuous);

}   // setup()


void loop() {}


////////////////////////////////////////////////////////////////////////////////////////
// display the game board
// 
void show()
{
    static char const icons[] PROGMEM = "pnbrqkPNBRQK";

    static const bool dev = true;

    long value = 0;

    index_t const offset = 0;

    for (unsigned char y = 0; y < 8; ++y) {
        printf(Debug1, "%c ", dev ? ('0' + y) : ('8' - y));
        for (unsigned char x = 0; x < 8; ++x) {
            Piece const piece = board.get(y * 8 + x);
            printf(Debug1, " %c ", 
                isEmpty(piece) ? ((y ^ x) & 1 ? '*' : '.') :
                pgm_read_byte(&icons[((getSide(piece) * 6) + getType(piece) - 1)]));
        }

        // display the extra status info on certain lines:
        switch (y) {
            // display the last move made if available
            case offset + 0:
                if (game.last_move.from != -1 && game.last_move.to != -1) {
                    printf(Debug1, "    Last Move: %c%c to %c%c", 
                        (game.last_move.from % 8) + 'A', 
                        '8' - (game.last_move.from / 8), 
                        (game.last_move.to   % 8) + 'A', 
                        '8' - (game.last_move.to   / 8) );
                }
                break;

            // display the time spent on the last move
            case offset + 1:
                if (0 == game.last_move_time) break;
                if (0 != game.last_moves_evaluated) {
                    char str_moves[16] = "";
                    ftostr(game.stats.move_stats.counter(), 0, str_moves);
                    char str_moves_per_sec[16] = "";
                    ftostr(game.stats.move_stats.moveps(), 2, str_moves_per_sec);
                    char str_time[16] = "";
                    ftostr(game.last_move_time, 0, str_time);
                    printf(Debug1, "    %s moves in %s ms (%s moves/sec)", 
                        str_moves, str_time, str_moves_per_sec);
                }
                break;

            // display the max ply depth we were able to reach
            case offset + 2:
                if (game.stats.move_stats.maxply > 0) {
                    printf(Debug1, "    Max ply depth reached: %d", 
                        game.stats.move_stats.maxply);
                }
                break;

            // display the pieces taken by White
            case offset + 4:
                printf(Debug1, "    Taken 1: ");
                for (int i = 0; i < game.white_taken_count; i++) {
                    Piece const piece = game.taken_by_white[i];
                    Piece const ptype = getType(piece);
                    Color const pside = getSide(piece);
                    printf(Debug1, "%c ", pgm_read_byte(&icons[(pside * 6) + ptype - 1]));
                }
                break;

            // display the pieces taken by Black
            case offset + 5:
                printf(Debug1, "    Taken 2: ");
                for (int i = 0; i < game.black_taken_count; i++) {
                    Piece const piece = game.taken_by_black[i];
                    Piece const ptype = getType(piece);
                    Color const pside = getSide(piece);
                    printf(Debug1, "%c ", pgm_read_byte(&icons[(pside * 6) + ptype - 1]));
                }
                break;

            // display the current score
            case offset + 7:
            {
                value = game.last_value;

                char str_score[16] = "";
                ftostr(value, 0, str_score);
                printf(Debug1, "    Board value: %8s %s", str_score, (value == 0) ? "" : 
                    (value  < 0) ? "Black's favor" : "White's favor");
            }
                break;
        }
        printf(Debug1, "%c", '\n');
    }
    printf(Debug1, "%s", 
        dev ? "   0  1  2  3  4  5  6  7\n\n" : "   A  B  C  D  E  F  G  H\n");

    set_led_strip();

}   // show()